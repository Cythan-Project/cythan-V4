class Morpion {

    Array<Val, 9> grid;

    Morpion new() {
        return Morpion {
            grid = Array<Val, 9> tmp
        };
    }

    set(Morpion self, Val pos, Val val) {
        self.grid.setDyn(pos, val);
    }

    Val getDyn(Morpion self, Val pos) {
        return self.grid.getDyn(pos);
    }
    Val get<TE>(Morpion self) {
        return self.grid.get<TE>();
    }

    display(Morpion self) {
        Val count = 0;
        loop {
            if count.equals(9) {
                '\n'.print();
                break;
            };
            if count.equals(3) || count.equals(6) {
                '\n'.print();
            };
            Val j = self.getDyn(count);
            if j.equalsZero() {
                '-'
            } else if j.equals(1) {
                'O'
            } else {
                'X'
            }.print();
            count.inc();
        }
    }

    play(Morpion self) {
        Val currentPlayer = 1;
        Val count = 9;
        self.display();
        loop {
            Val pos = Val.input();
            pos.dec();
            if pos.greater(8) {
                continue;
            };
            if self.getDyn(pos).equalsZero() {
                self.set(pos, currentPlayer);
                count.dec(); 
                self.display();
                if self.winner(currentPlayer) {
                    if currentPlayer.equals(1) {
                        'O'
                    } else {
                        'X'
                    }.print();
                    " won!".println();
                    break;
                };
                if count.equalsZero() {
                    "Equality!".println();
                    break;
                };
                currentPlayer = if currentPlayer.equals(1) {
                    2
                } else {
                    1
                };
            } else {
                "Invalid input!".println();
            };
        };
    }

    Bool winner(Morpion self, Val tocheck) {
        if self.get<0>().equals(tocheck) {
            if self.get<1>().equals(tocheck) && self.get<2>().equals(tocheck) {
                return Bool.true();
            };
            if self.get<3>().equals(tocheck) && self.get<6>().equals(tocheck) {
                return Bool.true();
            };
            if self.get<4>().equals(tocheck) && self.get<8>().equals(tocheck) {
                return Bool.true();
            };
        };
        if self.get<1>().equals(tocheck) && self.get<4>().equals(tocheck) && self.get<7>().equals(tocheck) {
            return Bool.true();
        };
        if self.get<2>().equals(tocheck) && self.get<4>().equals(tocheck) && self.get<6>().equals(tocheck) {
            return Bool.true();
        };
        if self.get<3>().equals(tocheck) && self.get<4>().equals(tocheck) && self.get<5>().equals(tocheck) {
            return Bool.true();
        };
        if self.get<6>().equals(tocheck) && self.get<7>().equals(tocheck) && self.get<8>().equals(tocheck) {
            return Bool.true();
        };
        return Bool.false();
    }
}